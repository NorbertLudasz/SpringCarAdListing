plugins {
    id 'java'
    id 'war'
}

//new additions start here (and id 'war' above)
def deploymentName = 'war-static-lnim2044'

def tomcatHome = System.getenv('CATALINA_HOME')
logger.quiet("Tomcat home detected as '${tomcatHome}'")

// ezzel a taszkkal telepítjük ki a wart a Tomcatre
task deploy(type: Copy, dependsOn: 'war') {

    // futás előtt megnézzük, hogy a Tomcat home-ja helyesen be van-e állítva
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    // a "war" task kimenetét másoljuk
    from war.archiveFile

    // átnevezzük a war állományt a megfelelő contextRoot kialakításáért
    rename war.archiveFileName.get(), "${deploymentName}.war"

    // a másolás célpontja a Tomcat "webapps" almappája
    into "${tomcatHome}/webapps"
}

// Tomcat kitelepítés törlése
task undeploy(type: Delete) {

    // futás előtt megnézzük, hogy a Tomcat home-ja helyesen be van-e állítva
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    // töröljük a kitelepített war file-t
    delete "${tomcatHome}/webapps/${deploymentName}.war"
}
// new additions end here

group 'edu.bbte.idde.lnim2044'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '5.0.0'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
    runtimeOnly group: 'ch.qos.logback', name:'logback-classic', version:'1.2.3'
    // JSON olvasást támogató függőség
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.0'
    implementation project(':lnim2044-backend')//dunno if i need this
    //implementation group: 'com.github.jknack', name: 'handlebars', version: '4.2.0'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.31'
}